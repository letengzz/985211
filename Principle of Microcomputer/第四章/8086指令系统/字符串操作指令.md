# 字符串操作指令

​	串操作指令实现对一串字符或数据的操作，分为**基本串操作指令**和**重复前缀指令**。基本串操作指令每执行一次只能处理一个数据，与重复前缀指令结合可以处理一串数据。

**串操作共同点**：

1. 源串一般存放在数据段(`DS`)，偏移地址由`SI`指定**，** 目标串在附加段(`ES`)，偏移地址由`DI`指定。
2. 每执行一次串操作后自动修改指针`SI`、`DI`。若方向标志`DF=0`，则每次操作后`SI`和`DI`自动加1(或加2)；若`DF=1`，则每次操作后`SI`和`DI`自动减1(或减2)修改。
3. 串长(字或字节个数)存放在`CX`中。

**注意**：在执行指令前必须`DS`、`ES`、`SI`、`DI`、`DF`、`CX`置好需要的值，它们是串操作指令的隐含操作数。

## 字符串串操作指令

### 字符串传送指令

**格式**： 

​	`MOVS	OP目，OP源`	(OP源必须说明操作数的类型，确定是字节/字传送。)

- `MOVSB`——字节传送
- `MOVSW`——字传送

**功能**：把位于数据段由`SI`指定的内存单元的字节/字数据传送到附加段由`DI`指定的内存单元，指令不影响状态标志位。

**过程**：

​			[DI]←[SI]  若`DF=0` SI←SI+1/2，DI←DI+1/2
​			[DI]←[SI]  若`DF=1` SI←SI - 1/2，DI←DI-1/2

**说明**：串传送指令常与无条件重复前缀连用

### 字符串比较指令

**格式**： 

​	`CMPS	OP目，OP源` 

- `CMPSB`——字节比较
- `CMPSW`——字比较

**功能**：把位于数据段由`SI`指定的字节/字数据与附加段由`DI`指定的字节/字数据进行比较，结果不保存，但影响状态标志位，并由`DF`状态决定`SI`、`DI`的修改方向。

**过程**：[SI] -[DI]   SI←SI±1/2，DI←DI±1/2

**说明**：串比较指令常与条件重复前缀连用，指令的执行不改变操作数，仅影响标志位。

### 字符串搜索指令

**格式**：    

​	`SCAS	OP`	(OP：目标操作数)
​	`SCASB`
​	`SCASW`

**功能**: 把`AL`/`AX`中的内容与附加段由`DI`指定的一个字节/字数据进行比较，结果不保存，但影响状态标志位，并由`DF`状态决定`DI`的修改方向。 

**过程**：字节——AL - [DI]，DI←DI±1
           字——AX - [DI]，DI←DI±2 

### 取字符串指令

**格式**：

​			`LODSB`   ——AL←[SI]，SI←SI±1
​            `LODSW`  ——AX←[SI]，SI←SI±2

**功能**：把位于数据段由SI指定内存单元的内容取到`AL`或`AX`中，并修改`SI`的内容，指向下一字节/字单元。 

### 存字符串指令

**格式**：

​			`STOSB` ——[DI]←AL，DI←DI±1
​            `STOSW` ——[DI]←AX，DI←DI±2

**功能**:  把寄存器`AL`或`AX`中的内容存到附加段由`DI`指定的内存单元，并修改`DI`的内容，指向下一字节/字的存放单元。


## 重复前缀指令

​    基本串操作指令每执行一次只能处理一个数据，与重复前缀指令结合可以处理一串数据。

![image-20220523101741755](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205282041643.png)

**注意**：

1. 重复前缀指令不能单独使用，其后必须紧跟基本串操作指令，控制基本串操作指令重复执行。其执行过程相当于一个循环程序的运行。在每次重复之后，地址指针`SI`和`DI`都被修改，但指令指针`IP`仍保持指向带有前缀的串操作指令的地址。
2. 重复执行次数由数据串长度决定，数据串长度应预置在寄存器`CX`中。
3. 执行重复前缀指令不影响标志位。

**例**：

1. 将从`DATA1`开始的100个字节的数据传送到从`DATA2`开始的存储区中。

   1. 用一般传送指令实现：

      ```
        MOV	SI,0FFSET  DATA1		
        MOV   DI,0FFSET  DATA2					
        MOV   CX,64H			
      PL1:MOV	AL,[SI]							
        MOV   [DI],AL	
        INC   SI				
        INC	DI				
        DEC	CX								
        JNZ	PL1          		
      DONE:HLT	
      ```

   2. 用串操作指令实现：

      ```
        MOV	SI,0FFSET  DATA1
        MOV	DI,0FFSET  DATA2			
        MOV	CX,64H					
        CLD              ;DF=0					
      PL1:MOVSB	
        DEC	CX							        
        JNZ	PL1           		
      DONE:HLT
      
      ```

   3. 用重复传送指令

      ```
        MOV	SI，OFFSET   DATA1	
        MOV	DI，OFFSET   DATA2	
        MOV	CX，64H
        CLD			
        REP	MOVSB				
        HLT	
      ```

2. 比较存储器中的两串数是否相同，如完全相同则`AL=0`，否则`AL=0FFH`。

   ```
     MOV	SI,0FFSET  DATA1
     MOV	DI,0FFSET  DATA2				      
     CLD						
     MOV	CX,50					  
     REPZ	CMPSB	
     JZ	EQQ           		
     MOV	AL,0FFH			
     JMP	STOP
   EQQ:MOV	AL,0		
   STOP:HLT		
   ```

3. 在某字串中查找是否存在字符“`$`”。

   ```
     MOV	DI,0FFSET  DATA1			   
     MOV	AL,'$'					          
     MOV	CX,64H			
     CLD	
     REPNZ	SCASB			
     JZ	FOUND
     MOV	BX,0 
     JMP	STOP
   FOUND:DEC	DI						
     MOV	BX,DI 
   STOP:HLT	
   ```
   
4. 将20个`0AAAAH`送入`3000H`开始的地址单元。 

   程序段如下：

   ```
     CLD
     LEA	DI,[3000H]
     MOV	CX,14H
     MOV	AX,0AAAAH
     REP	STOSW
   ```

5. 将某数据区的100个数中的正数、负数分开，分别送同一数据段的另外两个数据区中。

   ```
     MOV	SI,OFFSET  DATA1
     MOV	DI,OFFSET  DATA2 ;存正数
     MOV	BX,OFFSET  DATA3 ;存负数
     MOV	CX,64H
     CLD
   LP:LODSB
     TEST	AL,80H ;判断D7位是否为1(正、负数)
     JNZ	MIVS	;ZF=0(负数)转
     STOSB	;存正数
     JMP	NEXT	
   MIVS:XCHG	BX,DI
     STOSB	;存负数
     XCHG	BX,DI 
   NEXT:DEC CX
     JNZ	LP
     HLT
   ```

   

