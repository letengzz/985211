# 算数运算指令

​	8086的算术运算类指令能够对二进制或十进制(BCD码)数进行加、减、乘、除运算，操作数的数据形式可以是8位或16位的无符号数或带符号数。


## 加法指令

### 不带进位的加法指令

**格式**：

​	`ADD	OP目,OP源` 

**功能**：OP目 <-- OP源 + OP目，根据结果设置标志位

**注意**：两操作数的类型相同，类型明确，不能同为存储器操作数

**例**：

​	`ADD	AL,50H` 	`;AL+50H → AL`

​	`ADD	DI,SI`	`;DI+SI → DI`

​	`ADD	AX,[DI]`	`;(DI)、(DI+1)内容+AX → AX`

​	`ADD	[BX+DI],AX`	`;(BX+DI)和(BX+DI+1)2个单元的内容+AX，结果放在BX+DI和BX+DI+1所指单元`                            

​	`ADD	AX,[BX+2000H]`	`;BX+2000H和BX+2001H所指单元内容和AX的内容相加，结果在AX中`

### 进位的加法指令

**格式**：

​	`ADC	OP目,OP源`

**功能**：OP目 <-- OP源 + OP目+CF(CF为前面指令产生的CF)，置标志位

**说明**：主要用于**多字节运算**，多字节运算时低位字节产生的进位应加到高位。

**例**：

 1. `ADC	AX,SI`	`;AX+SI+CF → AX`

 2. 两个双字(32位)相加。`123FAB5H`+`0ABC212AH`

    `MOV	DX,0123H`

    `MOV	AX,0FAB5H`

    `ADD	AX,212AH`	`;先加低字，CF=1 AX=1BDFH`

    `ADC	DX,0ABCH`	`;高字带进位加 CF=0 DX=0BE0H`

 ![image-20220521102334152](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272021621.png)

3. 有两个4字节数分别放在自`2000H`和`3000H`开始的存储区中，低字节在低地址处，编一程序段将两数相加，结果存放于从`2000H`开始的单元中。

   ```
     MOV	SI,2000H
     MOV	AX,[SI]
     MOV	DI,3000H 
     ADD	AX,[DI]
     MOV	[SI],AX 
     MOV	AX,[SI+2] 
     ADC	AX,[DI+2] 
     MOV	[SI+2],AX
   ```

   <img src="https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272024903.png" alt="image-20220521104334110" style="zoom:67%;" />

4. 有两个4字节数分别放在自FIRST和SECOND开始的存储区中，低字节在低地址处，编一程序段将两数相加，结果存放于从FIRST开始的存储区中。

   ```
     MOV	AX,FIRST 
     ADD	AX,SECOND 
     MOV	FIRST,AX 
     MOV	AX,FIRST+2 
     ADC	AX,SECOND+2 
     MOV	FIRST+2,AX
   ```

   <img src="https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272025359.png" alt="image-20220521104553369" style="zoom:67%;" />

5. `0000 0001`+ `0111 1111`=`1000 0000`

   ![image-20220521105424143](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272026830.png)

   由结果可知：`CF=0`  `OF=1` `ZF=0` `SF=1`  `AF=1` `PF=0`

   以上运算可用两条指令实现：

   ​                         `MOV	AH,01`

   ​                         `ADD	AH,7FH`

### 加1指令

**格式**：

​	`INC	OP`     

**功能**：OP <-- OP+1

**说明**：常用于修改偏移地址和计数次数。操作数可以是8/16位通用寄存器或存储器操作数，不能为立即数。 

**注意**：

1. `INC`指令**不影响**`CF`位，影响标志位`AF`、`OF`、`PF`、`SF`和`ZF`。
2. 操作数视为无符号数。

**例**：

​	`INC	AL`	`;AL <-- AL+1`

​	`INC	BYTE PTR [BX+DI]`	`;[BX+DI]←[BX+DI]+1`

判断对错

​	`INC  CL`     √

​	`INC  [DI]`    ×   →   `INC BYTE PTR[DI]` **√**

​	`INC  2000H`  ×


### 总结

​	以上三条指令运算结果将影响状态标志位，但 `INC`不影响标志`CF`。

## 减法指令

### 不带借位的减法指令

**格式**：

​	`SUB	OP目,OP源`

**功能**： OP目← OP目-OP源 ，并根据结果设置标志
**例**：
	`SUB   BX,CX`	`;BX-CX → BX`
	`SUB   AL,[SI+2]`	`;AL-(SI+2)单元中的数 → AL`
	`SUB   AL,20`	`;AL-20 → AL`

### 带借位的减法指令

**格式**：

​	`SBB	OP目,OP源`

**功能**：OP目 ← OP目-OP源 -CF，根据结果设置标志
**说明**：主要用于多字节或多精度数据相减的运算

**例**： 

​	`SBB   AX,2030H`	`;AX- 2030H-CF → AX`                       
​	`SBB   WORD PTR [DI+2],1000H`	`;将DI+2和DI+3所指两单元的内容-1000H-CF， 结果存在DI+2和DI+3所指的单元`

### 减1指令

**格式**：

​	`DEC	OP`       

**功能**：OP←OP-1；根据结果置标志位，**不影响CF**。

**例**：
	`DEC	AX`	`;AX-1 → AX`
	`DEC	BL`	`;BL-1 → BL`
	`DEC	BYTE  PTR　[DI+2]`	`;将DI+2所指单元的内容减1，结果送回此单元`              

### 取补指令

**格式**： 

​	`NEG	OP`       

**功能**： 0- OP→ OP,  将操作数取补后送回原操作数 OP可以是8/16位通用寄存器和存储器操作数，不能为立即数。 

**例**：

​	`NEG	AL`	`;0- AL→ AL`

​	`MOV	AX,1`	`;AX=0001H`     

​	`NEG	AX`	`;AX=0FFFFH` 

**注意**： 

1. 对`80H`或`8000H`取补时，操作数没有变化，但`OF=1`。
2. 对`CF`影响较特殊，只要操作数不是0，总是使`CF=1`。

### 比较指令

**格式**：

​	`CMP	OP目,OP源` 

**功能**：OP目 - OP源，不回送结果，只根据结果置标志位。      

**说明**：本指令主要通过比较(相减)结果置标志位，表示两个操作数的关系，**指令执行的结果不影响目标操作数**。 
**用途**：用于比较两个数的大小，可作为条件转移指令转移的条件

**例**：
	`CMP	AX,BX`	`;AX-BX，根据结果置标志位`
	`CMP	AL,20H`	`;AL-20H，根据结果置标志位`

**比较的几种情况**(以`CMP	A,B`示例说明) ：

1. 判断两个操作数是否相等：

   ![image-20220522192111824](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272131122.png)

2. 判断两个操作数的大小，可分两种情况：

   1. 判断两个无符号操作数的大小：

      ![image-20220522192223973](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272131707.png)

   2. 判断两个带符号操作数的大小

      - 两操作数符号相同

        ![image-20220522192615236](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272132095.png)

      - 两操作数符号不同

        ![image-20220522192637104](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272132696.png)

**例**：

1. 有两组50个字节的数据，分别放在自FIRST和SECOND开始的存储区中，试比较两组数据是否相同。相同则0  →  AL，否则`0FFH`  →  AL。

   ```
     MOV	SI,OFFSET  FIRST 
     MOV	DI,OFFSET  SECOND 
     MOV	CX,50 
   P:MOV	AL,[SI] 
     CMP	AL,[DI] 
     JZ	NEXT 
     MOV	AL,0FFH
     JMP	STOP 
   NEXT:INC	SI 
     INC	DI 
     DEC	CX 
     JNZ	P 
     MOV	AL,0 
   STOP:HLT 
   ```

2. ```
      LEA	BX,MAX
      LEA	SI,BUF
      MOV CL,19
      MOV AL,[SI]
    NEXT:INC	SI
      CMP AL,[SI]
      JNC  GOON		;CF=0转
      XCHG  [SI],AL
    GOON:DEC  CL
      JNZ  NEXT
      MOV [BX],AL
    HLT

![image-20220522194544094](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272138614.png)


## 乘法指令

### 无符号数乘法

**格式**：

​	`MUL	OP`   

**功能**： `AX` <-- `AL×OP`           8位数乘法
			(`DX`、`AX`) <-- `AX×OP`    16位数乘法

### 带符号乘法

**格式**： 

​	`IMUL	OP`  

**功能**： 操作同上，但是**操作数为带符号数**

![image-20220522200524872](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272143876.png)

例：`MUL	BYTE PTR[BX]`

![image-20220522200816845](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272143163.png)



### 注意

1. 进行字节运算时， OP目必须是`AL`，乘积在`AX`中；进行字运算时， OP目必须是`AX`，乘积在`DX`、`AX`中。 源操作数不允许使用立即数寻址方式。

2. 运算结果**只影响`CF`、`OF`**，其他的无定义。

3. 对于`MUL`，若字节型数据相乘之积AH=0或字数据相乘之积`DX=0`，则`CF=OF=0`，否则`CF=OF=1`；

   对于`IMUL`指令，若字节数据相乘之积AH或字数据相乘之积`DX`的内容是低一半的符号扩展，则`CF=OF=0`，否则`CF=OF=1`。

## 除法指令

​	指令要求**被除数是除数的双倍字长**，即当除数是8/16位时，要求被除数是16/32位的二进制数。

### 无符号数除法

**格式**：

​	`DIV	OP`

![image-20220522202038588](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272144520.png)

### 带符号数除法

 **格式**：

​	`IDIV	OP`
 **功能**：操作同`DIV`，但是**操作数为带符号数**

![image-20220521112321833](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205272144382.png)

### 注意

1. 当除数是字节时，被除数必须放在`AX`中，
   当除数是字时，被除数必须放在`DX`，`AX`中。
2. 商超出规定的范围时，将产生0号中断。
3. `IDIV`运算结果余数的符号与被除数相同。
4. 带符号数除法运算中，当被除数位数不够时，则需将被除数扩展到所需的位数。（ 8086/8088设有带符号数扩展指令）

**例**：

​	`DIV	CL`	`;AX/CL，商→AL，余数→AH`

​	`DIV	WORD PTR [DI]`	`;DX和AX中32位数除以DI、DI+1所指的16位数，商 → AX，余数 → DX`          

​	`IDIV	BX`	`DX、AX/ BX，商 → AX，余数 → DX`                            

​	`IDIV	BYTE PTR[DI]`	`;AX中16位数除以DI所指单元中的8位数，商 → AL，余数 → AH`


## 符号扩展指令

**这两条指令不影响标志位。**

### 字节扩展指令

**格式**：

​	`CBW` 

**功能**：将`AL`中的数的符号位扩展到`AH`
**规则**：若最高位=1，则执行后`AH=FFH` 
               若最高位=0，则 执行后`AH=00H`

### 字扩展指令

**格式**： 

​	`CWD`   

**功能**：将`AX`中的数的符号位扩展到`DX`，
**规则**：若最高位=1，则执行后`DX=FFFFH`
                若最高位=0，则执行后`DX=0000H`

**例**：

​	分析指令执行后的结果

```
  MOV  AL,44H
  CBW
					结果：AX=0044H
  MOV  AX,0AFDEH
  CWD
					结果DX=FFFFH,    AX=0AFDEH
  MOV  AL,86H
  CBW
  					结果：AX=FF86H
```


## 十进制运算调整指令

**BCD码**：二进制编码的十进制数，又称为二—十进制数。

**非组合(非压缩)BCD码**：用一个字节表示一位十进制数。

**组合(压缩)BCD码**：用一个字节表示二位十进制数。

**例**：

![image-20220522214920101](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205281427995.png)

​	由于BCD码是四位二进制编码，四位二进制数共有16个编码，BCD码只用其中的10个，其余没用的编码1010~1111称为无效码。BCD码运算结果进入或跳过无效码区时，都会出现错误。为了得到正确结果，必须进行调整。

**调整原则**：运算结果>9 或 `D3`(`D7`)向高位有进位（借位），进行加6（或减6）调整。

**例**：18+27=45

![image-20220522215503719](https://cdn.jsdelivr.net/gh/letengzz/Two-C@main/img/PM/Fourth/202205281427859.png)

### 非组合BCD码的加法调整指令

**格式**：

​	`AAA` 

**功能**：对AL中的由两个非组合BCD码相加的和进行调整，结果（非组合BCD码）存于AX中。 

**调整过程**：若AL的低4位大于9或AF=1，则AL←AL+6， AF←1，CF←1，AH←AH+1，清除AL的高四位；否则清除AL的高4位以及AF和CF标志；

**注意**：指令使用时必须紧跟在`ADD`或`ADC`之后 


### 组合BCD码的加法调整指令

**格式**： 

​	`DAA`

**功能**：对AL中的由两个组合BCD码相加的和进行调整，将结果（组合BCD码）存于 AL中。
**调整过程**： 调整方法与AAA类似，只是此指令要分别考虑AL的高4位和低4位。
      若AL低4位大于9或AF=1，则AL+6→AL，置AF=1；
      若AL高4位大于9或CF=1，则AL+60H→AL，置CF=1

**注意**：指令使用时必须紧跟在`ADD`或`ADC`之后  

### 非组合BCD码的减法调整指令

**格式**： 

​	`AAS`	

**功能**：对AL中由两个非组合BCD码相减的差进行调整，将结果（非组合BCD码）存于AL中。
**调整过程**：调整方法与AAA类似，不同的是当AL的低4位表示的数大于9或AF=1时，将AL-6→AL，AH-１→AH，并将AF和CF置1，清除AL的高四位。 

**注意**：指令必须紧跟在减法指令之后

### 组合BCD码的减法调整指令

**格式**： 

​	`DAS`

**功能**：对AL中由两个组合BCD码相减所得的结果进行调整，并将结果（组合BCD码）存于AL中。
**调整过程**：调整方法与`DAA`类似，不同的是当AL的低4位>9或者 AF=1，则AL-6→AL，并置AF=1；而当 AL的高4位大于9或者 CF=1时，则AL-60H → AL，并置CF=1。

**注意**：指令必须紧跟在减法指令之后

### 非组合BCD码的乘法调整指令

**格式**： 

​	`AAM`	

**功能**：对AX中的由两个非组合BCD码相乘所得的结果进行调整，并将调整后的结果存于`AX`中。
**调整过程**：AH←AL / 0AH（商），AL←AL % 0AH(余数)
**注意**：本指令必须紧跟在`MUL`指令之后使用 。

### 非组合BCD码的除法调整指令

**格式**： 

​	`AAD`	

**功能**：把AX中的两个非组合BCD码进行调整，然后可用`DIV`指令实现两个非组合BCD码的除法运算 
**调整过程**： AL←10×AH＋AL，AH←0 
**注意**：本指令必须在**`DIV`运算前使用** 。 

### 总结

`DAA`或`DAS`必须用在`ADD`(`ADC`)或`SUB`(`SBB`)之后，结果对`OF`无影响，对其他状态标志位均有影响。
`AAA`或`AAS`必须用在`ADD`(`ADC`)或`SUB`(`SBB`)之后，结果影响`AF`和`CF`，对其他标志位均无意义。
`AAM`必须用在`MUL`之后，结果影响`SF`、`ZF`和`PF`，对`AF`、`CF`和`OF`无影响。
`AAD`必须用在`DIV`之前，结果影响`SF`、`ZF`和`PF`，对`AF`、`CF`和`OF`无影响。

**例**：

1. 设在`AX`和`BX`中分别存有用组合BCD码表示的十进制数，编写程序段将两个数相加，结果存于`AX`中。

   ```
     ADD	AL,BL	;低字节相加
     DAA			;低字节调整
     MOV	CL,AL	;暂存低字节
     MOV	AL,AH		
     ADC	AL,BH	;高字节相加
     DAA			;高字节调整
     MOV	AH,AL		
     MOV	AL,CL	;存放结果于AX中
   ```

2. 在`1000H`中开始的单元中，连续存放着4个压缩BCD码表示的两个十进制数，将两个数相加(相减)，结果送回`1000H`中。 

   ```
     MOV	SI,1000H
     MOV	AL,[SI]              
	  ADD	AL,[SI+2]	;低字节相加
	  DAA				;低字节调整
	  MOV	[SI],AL		;暂存低字节
	  MOV	AL,[SI+1] 		
	  ADC	AL,[SI+3]	;高字节相加
	  DAA				;高字节调整
	  MOV	[SI+1],AL	;存放结果于AX中	
	```
	
	





